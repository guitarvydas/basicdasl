OPML2FB {
  OPML = xmlHeader opmlHeader head body Outline endBody endOPML
  opmlHeader = "<opml" opmlHeaderChar* ">" newline
  xmlHeader = "<?xml" xmlHeaderChar* "?>" newline
  head = "<head>" headChar* "</head>" newline
  body = "<body>" newline
  endBody = "</body>" newline
  endOPML = "</opml>" (newline | end)

  Outline = OutlineNoContent | OutlineWithContent
  OutlineNoContent = "<outline" item "/>"
  OutlineWithContent = "<outline" item ">" Outline* "</outline>"

  item = noise | graphicalObject | attribute 
  graphicalObject = lineObject | arrowObject | circleObject | compObject | rectObject | cylinderObject
  attribute = colorAttr | strokeWidthAttr | textAttr | genericAttr
  
  noise = "text=\"lines" stringTail

  circleObject = "text=\"circle " stringTail
  compObject = "text=\"comp " stringTail
  rectObject = "text=\"rect " stringTail
  cylinderObject = "text=\"cyl " stringTail
  lineObject = "text=\"line " stringTail
  arrowObject = "text=\"arrow " stringTail

  colorAttr = "text=\"color=" stringTail
  strokeWidthAttr = "text=\"stroke-width=" stringTail
  textAttr = "text=\"text=" stringTail
  genericAttr = "text=" string
  
  headChar = ~"</head>" any
  opmlHeaderChar = ~">" any
  xmlHeaderChar = ~"?>" any
  newline = "\n"

  string = dq notDQ* dq
  stringTail = notDQ* dq
  dq = "\""
  notDQ = ~"\"" any
}

  OPML [xmlhdr opmlhdr head body outline endbody endopml] =
    {{ scopeAdd ('counter', 0); }}
    [[${xmlhdr}${opmlhdr}${head}${body}${outline}${endbody}${endopml}]]
  opmlHeader [begin @hdr close nl] = [[]]
  xmlHeader [begin @hdr close nl] = [[]]
  head [begin @hdr close nl] = [[]]
  body [begin nl] = [[]]
  endBody [begin nl] = [[]]
  endOPML [begin nl] = [[]]

  Outline [o] = [[${o}]]
  OutlineNoContent [begin t slashClose] = [[${t}\n]]
  OutlineWithContent [begin t close @o slasho] = {{scopeAdd ("gobject", "id" + gen ())}} [[${t}${o}]]

  headChar [c] = [[${c}]]
  opmlHeaderChar [c] = [[${c}]]
  xmlHeaderChar [c] = [[${c}]]
  newline [c] = [[${c}]]

  item [i] = [[${i}]]
  graphicalObject [o] = [[${o.trim ()}\n]]
  attribute [a] = [[${a.trim ()}\n]]

  circleObject [teqc str] = [[circle(${scopeGet ("gobject")}, ${str}).]]
  compObject [teqc str] = [[comp(${scopeGet ("gobject")}, ${str}).]]
  rectObject [teqc str] = [[rect(${scopeGet ("gobject")}, ${str}).]]
  cylinderObject [teqc str] = [[cylinder(${scopeGet ("gobject")}, ${str}).]]
  lineObject [teqc str] = [[line(${scopeGet ("gobject")}, ${str}).]]
  arrowObject [teqc str] =
  [[
arrowBegin(${scopeGet ("gobject")}, ${abegin (str)}).
arrowEnd(${scopeGet ("gobject")}, ${aend (str)}).]]

  colorAttr [teqc str] = [[color(${scopeGet ("gobject")}, ${str}).]]
  strokeWidthAttr [teqc str] = [[strokeWidth(${scopeGet ("gobject")}, ${str}).]]
  textAttr [teqc str] =
    {{ scopeAdd("tobject", "tid" + gen ()) }}
    [[str(${scopeGet ("tobject")}, ${changeUnicodeQuotes (str)}).\ntext(${scopeGet ("gobject")}, ${scopeGet ("tobject")}).]]
  genericAttr [teq str] = [[${teq}${str}]]

  string [q1 @cs q2] = [[${q1}${changeUnicodeQuotes (cs)}${q2}]]
  stringTail [@cs q2] = [[${changeUnicodeQuotes (cs)}${q2}]]
  notDQ [c] = [[${c}]]
  dq [c] = [[]]

  noise [a b] = [[]]